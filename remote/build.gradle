apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'

configurations {
    compile.exclude module: 'domain'
}

dependencies {
    implementation project(':data')
    implementation libraryDependencies.kotlin
    implementation libraryDependencies.coroutines
    implementation libraryDependencies.gson
    implementation libraryDependencies.retrofit
    implementation libraryDependencies.retrofitGson
    implementation libraryDependencies.okhttpLogging
    implementation libraryDependencies.koinCore
    implementation libraryDependencies.koinExt

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
    testImplementation testDependencies.mockitoInline
    testImplementation androidTestDependencies.koinTest
}

jacoco {
    toolVersion = jacocoVersion
}

task testRemote(dependsOn: [':data:compileJava', ':remote:compileJava', ':remote:test']) {
    group = 'Test'
    description = "Run remote tests"
}

task jacocoTestReportRemote(type: JacocoReport, dependsOn: [
        'testRemote',
        'jacocoTestReport',
        'jacocoTestCoverageVerification'
]) {
    group = 'Test'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugJava = fileTree(dir: "${buildDir}/intermediates/classes/debugUnitTest", excludes: fileFilter)
    def debugKotlin = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugJava, debugKotlin]))
    getExecutionData().setFrom(
            fileTree(dir: "$buildDir", includes: [
                    "jacoco/testDebugUnitTest.exec",
                    "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec"
            ])
    )
}